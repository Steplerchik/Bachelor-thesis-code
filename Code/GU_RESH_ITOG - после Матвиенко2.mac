!                   Решаем
/SOL
  
ANTYPE,0
EQSLV,SPAR 


csys,0
lsel,u,loc,y,-Rplus,Rplus-0.25*H-mini
lsel,u,loc,y,Rplus,Rplus-0.25*H+mini
lsel,u,loc,z,0,H/2
DL,ALL,,UZ,
DL,ALL,,UX,
LSEL,ALL

lsel,u,loc,y,Rplus,-(Rplus-0.25*H-mini)
lsel,u,loc,y,-Rplus,-(Rplus-0.25*H+mini)
lsel,u,loc,z,0,H/2
DL,ALL,,UZ,
DL,ALL,,UX,
LSEL,ALL

CSYS,0
ASEL,S,LOC,Y,0
ASEL,U,LOC,Z,H-1.2*a,H-0.8*a
DA,ALL,UY,
ASEL,ALL

CSYS,1
NSEL,S,LOC,Y,0
NSEL,U,LOC,Z,mini/4,H
*GET, noc, node, 0, count 
CSYS,0
F,ALL,FZ,(1/2)/noc
NSEL,ALL

CSYS,1
NSEL,S,LOC,Y,180
NSEL,U,LOC,Z,mini/4,H
*GET, noc1, node, 0, count
CSYS,0 
F,ALL,FZ,(1/2)/noc1
NSEL,ALL

/STATUS,SOLU
SOLVE

FINISH  
/POST1  
!*  
/EFACET,1   
PLNSOL, S,EQV, 0,1.0

! Обозначаю диаметры, которыми буду пользоваться
CSYS,0
NSEL,S,LOC,Y,0
NSEL,U,LOC,Z,0,H-a-0.5*0.25*0.025*a
NSEL,U,LOC,Z,H,H-a+0.5*0.25*0.025*a
 NSEL,U,LOC,X,0,-(D+delta)/2

CSYS,1
*GET, nc, node, 0, count            
*GET, n_min, node, 0, num, min

*dim,radiusm,array,nc,1
radiusm(nc,1)=(D+delta)/2
*dim,radius,array,nc-(NINT(NINT(delta/el)/2)*2-1),1

!СОЗДАЕМ МАССИВ РАДИУСОВ

*do,i,1,nc-1,

*SET,n_num,n_min
*SET,maxim,0
*do,j,1,nc,
*GET, rad, NODE, n_num, LOC, X
*IF,rad,GT,maxim,THEN
*IF,radiusm(nc+1-i,1),GT,rad,THEN
    *SET,maxim,rad
  *ENDIF
  *ENDIF
n_num=NDNEXT(n_num)                   ! Получаем следующий номер узла в активном наборе, имеющий большее число, чем n_num
*enddo
 
radiusm(nc-i,1)=maxim
*enddo
!                                  убираем лишние точки
*SET,check,0
*SET,k,0
*do,i,1,nc
  *IF,check,EQ,0,THEN
  k=k+1
  radius(k,1)=radiusm(i,1)
  check=1
    *ELSE
  check=0
  *ENDIF 
*enddo
!                                   ПОЛУЧИЛ РАДИУС

!                           ВЫЧИСЛЯЕМ КИН по радиусам МЕТОДОМ МИНИМУМА КВАДРАТОВ


NSEL,ALL
CSYS,1
*DIM,kappa,,2
*SET,G,E/(2*(1+mu))

*SET,pi,3.141592653589793238462643
*SET,nc,nc-(NINT(NINT(delta/el)/2)*2-1)   !Перезадал nc


                                          ! ТУТ МОЕ ТВОРЧЕСТВО

CSKP,101,1,16,29,31,1,1,     ! 101 - Новая локальная ЦИЛИНДРИЧЕСКАЯ СК
CSKP,100,0,16,29,31,1,1,     ! 100 - Новая локальная ДЕКАРТОВАЯ СК

Rvnesh=0.1*a    ! Делаем внешний радиус 20%
Rvnutr=0.03*a   ! Делаем внутренний радиус 3%


tolshina=NINT(nc/2)
!       Выбираем элементы в области
allsel
csys,1
nsel,u,loc,y,-45,45
ESLN,U,1

nsel,all
NSEL,U,LOC,X,radius(tolshina)
ESLN,U,1

nsel,all
CSYS,101
NSEL,U,LOC,X,Rvnutr,Rvnesh
ESLN,U,1

*GET, LL, elem, 0, count             !количество элементов используй elnext(l_numm)
*GET, l_numm, elem, 0, num, min   !начальное мин значение элемента

*DIM,sred_uzl,ARRAY,LL,12

*do,i,1,LL,
*GET,number,ELEM,l_numm,node,9
sred_uzl(i,1)=number
*GET,number,ELEM,l_numm,node,10
sred_uzl(i,2)=number
*GET,number,ELEM,l_numm,node,11
sred_uzl(i,3)=number
*GET,number,ELEM,l_numm,node,12
sred_uzl(i,4)=number
*GET,number,ELEM,l_numm,node,13
sred_uzl(i,5)=number
*GET,number,ELEM,l_numm,node,14
sred_uzl(i,6)=number
*GET,number,ELEM,l_numm,node,15
sred_uzl(i,7)=number
*GET,number,ELEM,l_numm,node,16
sred_uzl(i,8)=number
*GET,number,ELEM,l_numm,node,17
sred_uzl(i,9)=number
*GET,number,ELEM,l_numm,node,18
sred_uzl(i,10)=number
*GET,number,ELEM,l_numm,node,19
sred_uzl(i,11)=number
*GET,number,ELEM,l_numm,node,20
sred_uzl(i,12)=number
l_numm=elnext(l_numm)
*enddo



!          Выбрали область точек
CSYS,1
NSEL,ALL
NSEL,S,LOC,Y,-45,45
NSEL,U,LOC,X,0,radius(tolshina)-0.25*el
NSEL,U,LOC,X,radius(tolshina)+0.25*el,D

CSYS,101
NSEL,U,LOC,X,Rvnesh,D
NSEL,U,LOC,X,0,Rvnutr

 
!
! Попробуем другую область
!CSYS,101
!NSEL,U,LOC,Y,10,350

NPLOT

*GET, L, node, 0, count            !тут количество узлов
*GET, l_num, node, 0, num, min     !минимальный узел

*DIM,rr_,,L
*DIM,tt_,,L
*DIM,ssx_,,L
*DIM,ssy_,,L
*DIM,txy_,,L

                !ДАННЫЕ
i=0
*do,tochki,1,L,

*IF,l_num,NE,0,THEN
rrr=0
ttt=0
sssx=0
sssy=0
ttxy=0
parapaparam=0
*do,j,1,LL,
*do,k,1,12,
*IF,l_num,EQ,sred_uzl(j,k),THEN
parapaparam=1
*ENDIF
*enddo
*enddo

*IF,parapaparam,NE,1,THEN
i=i+1
CSYS,101
*GET, rrr, NODE, l_num, LOC, X
rr_(i)=rrr
*GET, ttt, NODE, l_num, LOC, Y
tt_(i)=ttt/180*pi              ! в радианах                   
RSYS,100
*GET,sssx,NODE,l_num,S,X
ssx_(i)=sssx
*GET,sssy,NODE,l_num,S,Y
ssy_(i)=sssy
*GET,ttxy,NODE,l_num,S,XY
txy_(i)=ttxy
*ENDIF

*ENDIF
l_num=NDNEXT(l_num)
*enddo

*DIM,rr,,i
*DIM,tt,,i
*DIM,ssx,,i
*DIM,ssy,,i
*DIM,txy,,i

*do,ii,1,i,
rr(ii)=rr_(ii)
tt(ii)=tt_(ii)
ssx(ii)=ssx_(ii)
ssy(ii)=ssy_(ii)
txy(ii)=txy_(ii)
*enddo

L=i  ! Перезадал L


!          Задаем вектор SIGMA
*DIM,SIGMA,,3*L
*SET,check,1
i=0

*do,j,1,3*L,
*IF,check,EQ,1,THEN
i=i+1
SIGMA(j)=ssx(i)
check=2
*ELSE

*IF,check,EQ,2,THEN
SIGMA(j)=ssy(i)
check=3
*ELSE
SIGMA(j)=txy(i)
check=1
*ENDIF

*ENDIF 
*enddo
!         Вектор SIGMA готов

!                                         Тут мы работаем с М
chlen=100
BETTA=0
KINA=0
TNAPRA=0
*DIM,otkl,,chlen
*DIM,otklon,,chlen
*DIM,bet,,chlen
*DIM,ki,,chlen
*DIM,tn,,chlen

*do,M,2,chlen,                                                  


!              Задаем матрицу S
*DIM,S,ARRAY,3*L,M
*SET,check,1
i=0

*do,j,1,3*L,
*IF,check,EQ,1,THEN
i=i+1
*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((2+n/2+(-1)**n)*cos((n/2-1)*tt(i))-(n/2-1)*cos((n/2-3)*tt(i)))
*enddo
check=2
*ELSE

*IF,check,EQ,2,THEN
*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((2-n/2-(-1)**n)*cos((n/2-1)*tt(i))+(n/2-1)*cos((n/2-3)*tt(i)))
*enddo
check=3
*ELSE

*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((n/2-1)*sin((n/2-3)*tt(i))-(n/2+(-1)**n)*sin((n/2-1)*tt(i)))
*enddo
check=1
*ENDIF

*ENDIF 
*enddo

!          Матрица S готова



!         Задаем матрицу BM
*DIM,BM,ARRAY,M,M

*do,n1,1,M,
*do,n2,1,M,

BM(n1,n2)=0
*do,j,1,3*L
BM(n1,n2)=BM(n1,n2)+S(j,n1)*S(j,n2)
*enddo

*enddo
*enddo
!         Матрица BM готова

!         Задаем вектор С
*DIM,C,,M

*do,n1,1,M,

C(n1)=0
*do,j,1,3*L
C(n1)=C(n1)+S(j,n1)*SIGMA(j)
*enddo

*enddo
!         Вектор С задан


*DIM,AM,,M
*MOPER,AM(1),BM(1,1),SOLV,C(1)

KIN=AM(1)*SQRT(2*pi)     !МПа*корень из мм
TNAPR=AM(2)*4            !МПа
bet(M)=TNAPR*SQRT(pi*a)/KIN

ki(M)=KIN*SQRT(0.001) !МПа*корень из м
tn(M)=TNAPR           !МПа

! Достали коэффициент бетта по центру фронта



! Ищем отклонение

podkor=0
podkoron=0

*do,j,1,3*L

sigm123will=0
*do,n,1,M
sigm123will=sigm123will+S(j,n)*AM(n)
*enddo

podkor=podkor+((sigm123will-SIGMA(j))/SIGMA(j))**2

podkoron=podkoron+abs((sigm123will-SIGMA(j))/SIGMA(j))
*enddo

otkl(M)=podkor/(3*L)
otklon(M)=podkoron/(3*L)

! Нашли отклонение

!МЕТКИ
*IF,otklon(M),LE,0.10,THEN

BETTA=bet(M)
KINA=ki(M)
TNAPRA=tn(M)
imin=M

*exit
*endif


! тут удаляем массивы

*del,s
*del,bm
*del,c
*del,am

*enddo

*IF,BETTA,EQ,0,THEN
imin=0
minn=1000
*do,i,2,M,
*IF,otklon(i),LE,minn,THEN
minn=otklon(i)
imin=i
*endif
*enddo

BETTA=bet(imin)
KINA=ki(imin)
TNAPRA=tn(imin)
ot=otkl(imin)
*endif



